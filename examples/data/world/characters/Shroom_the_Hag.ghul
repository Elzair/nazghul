//
// Copyright-Only Dedication (based on United States law)
//
// The person or persons who have associated their work with this document (the
// "Dedicator") hereby dedicate the entire copyright in the work of authorship
// identified below (the "Work") to the public domain.
//
// Dedicator makes this dedication for the benefit of the public at large and
// to the detriment of Dedicator's heirs and successors. Dedicator intends this
// dedication to be an overt act of relinquishment in perpetuity of all present
// and future rights under copyright law, whether vested or contingent, in the
// Work. Dedicator understands that such relinquishment of all rights includes
// the relinquishment of all rights to enforce (by lawsuit or otherwise) those
// copyrights in the Work.
//
// Dedicator recognizes that, once placed in the public domain, the Work may be
// freely reproduced, distributed, transmitted, used, modified, built upon, or
// otherwise exploited by anyone for any purpose, commercial or non-commercial,
// and in any way, including by methods that have not yet been invented or
// conceived.
//

#ifndef Shroom_the_Hag_ghul
#define Shroom_the_Hag_ghul

#include "../conversations/C_green_tower.ghul"

RESP Shrooms_Name_Resp {
        SAY "I'm known as Shroom. At your service."
}

RESP Shrooms_Join_Resp {
        SAY "I'm too old for you, sweetie!";
}

RESP Shrooms_Default_Resp {
        SAY "I might have known something about that long ago...";
}

RESP Shrooms_Shroom_Resp {
        SAY "Mushrooms are my specialty. That's why they call me Shroom.";
}

RESP Shrooms_Mushroom_Resp {
        CHECK_PARM ACTIVITY = WORKING {
                SAY "Yes, I trade in mushrooms and other reagents. Interested?";
                GET_YES_NO {
                        TRADE {
                                reagent_sulphorous_ash 2;
                                reagent_garlic         2;
                                reagent_ginseng        2;
                                reagent_blood_moss     8;
                                reagent_black_pearl    8;
                                reagent_spider_silk    8;
                                reagent_mandrake       16;
                                reagent_nightshade     16;
                                t_sleep_potion         10;
                        }
                        SAY "Be careful with those.";
                }{
                        SAY "Don't try to pick your own. Kill you, the bad ones will!";
                }
        } {
                SAY "Yes, I trade in mushrooms and other reagents. My shops in the northeast corner of town. Come by when I'm open."
        }
}

RESP Shroom_Gen {
        SAY "Aye, a handsome young man he was, once. He could stay up all night in bed! But gone a bit strange, he has, befriending the goblins and all that.";
}

RESP Shroom_strange {
        SAY "He meets them in the forest and runs with their hunts. Half-goblin he nearly is; learned their ways he has. But ye could never tell him what to do, the silly man.";
}

RESP Shroom_goblins {
        SAY "I trade with them now and then. Their shamans know well the plants in these woods. I even speak a little. Know some of their magic. But I never trust them.";
}

RESP Shroom_trust {
        SAY "The goblins will turn on us when their opportunity comes. I would do the same in their place.";
}

RESP Shroom_wars {
        SAY "Ha! Yes, I fought the goblins. Long ago that was. People forget."
}

RESP Shroom_maiden {
        SAY "[she grins with crooked teeth] Is it so hard to believe I was once a fair war-maiden? [she cackles obscenely]";
}

RESP Shroom_wards {
        CHECK_FLAG GAVE_QUEST {        
                CHECK_FLAG QUEST_COMPLETE {
                        SAY "I've forgotten all the others. But long ago the librarian from Glasdrin had me teach him and he wrote them all down. Check with him.";
                }{ // !  QUEST_COMPLETE
                        SAY "Bring me the mushrooms and I will teach ye the ward of panic. Do ye remember where they are?";
                        GET_YES_NO {
                                SAY "Well...";
                        }{
                                SAY "[sigh] Perhaps ye should write this down. Leave town and take the path north to the open plain and travel west until ye reach the mountains. Nearby will be the cave entrance. In the cave ye will find the mushrooms.";
                        }
                }  // !  QUEST_COMPLETE
        }{ // ! GAVE_QUEST
                SAY "In my time I knew many battle wards. Be wanting me to teach ye, now, won't ye?";
                GET_YES_NO {
                        SAY "I know a battle ward that will throw your enemies into a panic. But perhaps ye can do me a favor first, yes?";
                        GET_YES_NO {
                                SAY "In a cave to the west in the mountains grows a purple mushroom. Bring me four. Agreed?";
                                GET_YES_NO {
                                        SAY "Good. A colony of slimes infests that cave, so take plenty of flaming oil!";
                                        SET_FLAG GAVE_QUEST;
                                }{
                                        SAY "Yes, perhaps ye are afraid.";
                                        }
                        }{
                                SAY "Naught for naught, youngling!";
                        }
                }{
                        SAY "Of course, a skillfull warrior such as you has nothing to learn from an old witch like me.";
                        
                }
        } // ! GAVE_QUEST
}

RESP Shroom_Glasdrin {
        SAY "I don't know where Glasdrin or the librarion are. Not in these woods, anyway.";
}

RESP Shroom_hail {
        CHECK_FLAG GAVE_QUEST {
                CHECK_FLAG QUEST_COMPLETE {
                        SAY "Hello again, young wanderer. Come visit a bit with old Shroom.";
                }{
                        CHECK_ITEM PurpleMushroom > 3 {
                                SAY "Ah, ye have the mushrooms, as I requested!";
                                TAKE_ITEM PurpleMushroom 4;
                                SET_FLAG QUEST_COMPLETE;
                                SAY "Now for your reward. The battle ward is called In Quas Corp, of the seventh circle. Mix nightshade, mandrake and garlic. Cast it in battle and your enemies will flee in terror!";
                        }{
                                SAY "No purple mushrooms yet, I see. No rush, dear. But I would like them before I die.";
                        }
                }
        }{
                SAY "Hello and well met.";
        }
}

RESP Shroom_abigail {
        SAY "One needed a child, one needed a mother. Two demand justice, one thinks they have escaped it, two await to see if one will pursue it."
}

RESP Shroom_justice {
        SAY "It will begin with the child, the Daughter of the Forest.";
}

CONV Shrooms_Conv {
        IMPORT { 
                c_generic;
                GreenTower_Common_Conv;
        }
        NATIVE {
                BUY      Shrooms_Mushroom_Resp;
                DEFAULT  Shrooms_Default_Resp;
                GEN      Shroom_Gen;
                GLASDRIN Shroom_Glasdrin;
                GOBLINS  Shroom_goblins;
                HAIL     Shroom_hail;
                JOIN     Shrooms_Join_Resp;
                MAIDEN   Shroom_maiden;
                MUSHROOM Shrooms_Mushroom_Resp;
                NAME     Shrooms_Name_Resp;
                SHROOM   Shrooms_Shroom_Resp;
                STRANGE  Shroom_strange;
                TRUST    Shroom_trust;
                WARDS    Shroom_wards;
                WARS     Shroom_wars;
                ABIGAIL  Shroom_abigail;
        }
        
}

SCHED Shrooms_Sched {
        // Specific to Green Tower:
     // hr mn x  y  w  h  act
        0  0  51 9  1  1  SLEEPING;
        8  0  40 11 3  3  IDLE;      // picking shrooms
        9  0  49 6  7  1  WORKING;
        12 0  50 9  1  1  EATING;
        13 0  49 6  7  1  WORKING;
        18 0  56 54 1  1  EATING;
        19 0  53 50 4  7  IDLE;     // hanging out
        21 0  51 9  1  1  SLEEPING;
}

CHAR Shroom_the_Hag {
        name      "Shroom";
        species   human;
        occ       druid;
        sprite    s_companion_druid;

        // Personal stat modifiers
        str       0;
        intl      0;
        dex       0;

        // Combat modifiers
        hp_mod  0;
        hp_mult 0;
        mp_mod  0;
        mp_mult 0;
        hit_mod 0;
        def_mod 0;
        dam_mod 0;
        arm_mod 0;

        hp        30;
        xp        10000;
        cond      G;
        magic     150;
        lvl       9;

        conv      Shrooms_Conv;
        readied {
        }
        sched     Shrooms_Sched;
}

#endif
